package (
    AtumObject,
    updateAccordions,
    create,
    createForKey)
with
    import 'knockout-2.2.1' ko,
    
    import 'atum/compute' {just},
    
    import 'sheut/debug' debug,
    import 'sheut/run' run,
    import 'sheut/fun' {map},
    import 'sheut/operations/object' object,
    import 'sheut/operations/reference' reference
in {

static $;


var noop = \->{};

updateAccordions = \() ->
    $('.object-browser').each(\ ->
        ($(this).hasClass('ui-accordion') ?
            $(this) :
            $(this).accordion({
                'collapsible': true,
                'animate': 100,
                'heightStyle': "content",
                'active': false
            })));
/* 
 ******************************************************************************/
var AtumChild = function(key, value) {
    var self = this;

    self.key = ko.observable(key);
    self.value = ko.observable(value);
};

AtumObject = function(d, x) {
    var self = this;
    
    var value = run.extract(d, reference.getValue(x), null);
    
    self.value = ko.observable(value);
    self.children = ko.observableArray();
    self.isObject = ko.observable(value && value.type === 'object');
    
    self.getChildren = \data, event -> {
        var self = this;
        var value = self.value();
        
        if (!self.isObject())
            return;
        
        if (!this.children().length) {
            run.extract(d, object.getOwnPropertyNames(x), null,
                (map, \key ->
                    run.extract(d,
                        object.get(x, key),
                        null,
                        \value -> self.children.push(new AtumChild(key, new AtumObject(d, value))))));
        }
        
        if (event)
            updateAccordions();
    };
};


create = \value, ctx -> {
    var obj = new AtumObject(debug.debug(just(value), ctx, noop, noop), value);
    obj.getChildren();
    return obj;
};

createForKey = \key, value, ctx ->
    new AtumChild(key, create(value, ctx));

}