package (
    Result,
    Input,
    
    ConsoleViewModel)
with
    import 'knockout-2.2.1' ko,
    
    import 'amulet/record' record,
    
    import 'atum/compute' compute,
    import 'atum/operations/object' object,
    
    import 'sheut/debug' debug,
    import 'sheut/run' run,
    import 'sheut/step' step,
    import 'sheut/operations/context' context,
    import 'sheut/operations/reference' reference,
    
    import 'atum_debug_console/object_explorer' object_explorer{AtumObject}
in {

static $;

var map = \f, a -> Array.prototype.map.call(a, f);

var noop = \x -> x;

/* Console
 ******************************************************************************/
Result = record.declare(null, [
    'value',
    'error']);
Result.prototype.type = 'result';

Input = record.declare(null, [
    'input']);
Input.prototype.type = 'input';

/* Environment
 ******************************************************************************/
var Environment = function(bindings) {
    this.bindings = ko.observableArray(bindings);
};

var Binding = function(name, value) {
    this.name = name;
    this.value = value;
};

var printBindings = \d, environment -> let
    bindings = run.extract(d, context.getEnvironmentBindings(environment), [])
in
    bindings.reduce(\p, c -> {
        p.push(new Binding(c, run.extract(d, context.getEnvironmentBinding(environment, c), null, \x, ctx -> object_explorer.createForKey(c, x, ctx))));
        return p;
    }, []);

var printFrame = \d, lex -> new Environment(printBindings(d, lex));

var printEnvironments = function(d) {
    var environments = [];
    var environment = run.extract(d, context.environment, null);
    do {
        environments.push(printFrame(d, environment));
        environment = run.extract(d, context.getEnvironmentOuter(environment), null);
    } while (environment);
    
    return environments;
};


/* Stack
 ******************************************************************************/
var StackFrame = function(name) {
    this.name = name;
};

var createStackFrame = \d, frame ->
    new StackFrame(
        run.extract(d, context.getStackFrameName(frame), '', \x -> x.value));

var printStack = \d -> 
    run.extract(
        d,
        context.stack,
        [],
        (map, (createStackFrame, d)));

/* ConsoleViewModel
 ******************************************************************************/
ConsoleViewModel = function() {
    var self = this;
    
    self.debug = ko.observable();
    
    self.output = ko.observableArray();
    
    self.location = ko.computed <|\->
        (self.debug() ?
            run.extract(self.debug(), context.location, null) :
            []);

    self.environments = ko.computed <|\->
        (self.debug() ?
            printEnvironments(self.debug()) :
            []);
    
    self.stack = ko.computed <|\->
        (self.debug() ?
            printStack(self.debug()) :
            []);
};

ConsoleViewModel.prototype.finish = \() ->
    this.debug(step.finish(this.debug()));

ConsoleViewModel.prototype.run = \() ->
    this.debug(step.run(this.debug()));

ConsoleViewModel.prototype.stepOver = \() ->
    this.debug(step.stepOver(this.debug()));

ConsoleViewModel.prototype.stepInto = \() ->
    this.debug(step.step(this.debug()));

ConsoleViewModel.prototype.stepOut = \() ->
    this.debug(step.stepOut(this.debug()));

ConsoleViewModel.prototype.stop = \() ->
    this.debug(null);

ConsoleViewModel.prototype.pushResult = \value, ctx, error ->
    this.output.push(
        Result.create(object_explorer.create(value, ctx), error));

ConsoleViewModel.prototype.pushInput = \input ->
    this.output.push(
        Input.create(input));


ConsoleViewModel.prototype.selectFrame = \item -> {
    this.environment();
};

}